
buildscript {
	repositories {
		gradlePluginPortal()
		maven {
			name = "Fabric"
			url = "https://maven.fabricmc.net/"
		}
		flatDir {
			dirs 'libs'
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:${loomVersion}"
		classpath "com.github.johnrengelman:shadow:8.1.1"
	}
}

apply plugin: "fabric-loom"
apply plugin: "com.github.johnrengelman.shadow"

configurations {
	shadow
	modImplementation.extendsFrom shadow
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
	mavenLocal()
	maven {
		name = "lukflug"
		url = "https://lukflug.github.io/maven/"
	}
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
}


loom {
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft21}"
	mappings "net.fabricmc:yarn:${mappings21}:v2"
	modImplementation "net.fabricmc:fabric-loader:${loader21}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${api21}"
	shadow "com.lukflug:panelstudio:${version}"
	shadow "com.lukflug:panelstudio-mc21:${versionmc}"
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

shadowJar {
	archiveClassifier = "dev"
	configurations = [project.configurations.shadow]
	relocate "com.lukflug.panelstudio","com.lukflug.panelstudio_0_2_4"
}

remapJar {
	dependsOn shadowJar
	inputFile = shadowJar.archiveFile
}
