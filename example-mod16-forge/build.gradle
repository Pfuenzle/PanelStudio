
buildscript {
	repositories {
		maven {
			name = "forge"
			url = "https://maven.minecraftforge.net/"
		}
		gradlePluginPortal()
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:${forgeGradle5}"
		classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
	}
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "com.github.johnrengelman.shadow"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

configurations {
	extract
	compileClasspath.extendsFrom extract
	shadow
	implementation.extendsFrom shadow
}

repositories {
	maven {
		name = "forge"
		url = "https://maven.minecraftforge.net/"
	}
	maven {
		name = "lukflug"
		url = "https://lukflug.github.io/maven/"
	}
}

minecraft {
	mappings("snapshot",project.mappings16forge)
	runs {
		client {
			workingDirectory project.file("run")
			property "forge.logging.markers","REGISTRIES"
			property "forge.logging.console.level","debug"
		}
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft16forge}"
	shadow "com.lukflug:panelstudio:${version}"
	extract "com.lukflug:panelstudio-mc16-forge:${versionmc}"
}

processResources {
	inputs.property "version",project.version
	filesMatching("META-INF/mods.toml") {
		expand "version": project.version
	}
}

task extract (type:Copy) {
	from({zipTree(configurations.extract.singleFile)}) {exclude "META-INF/"}
	into "$buildDir/classes/java/main"
}

compileJava.finalizedBy("extract")

shadowJar {
	classifier = ""
	configurations = [project.configurations.shadow]
	relocate "com.lukflug.panelstudio","com.lukflug.panelstudio_0_2_4"
}

reobf {
	shadowJar{}
}
